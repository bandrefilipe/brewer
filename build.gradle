plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'org.sonarqube' version '3.0'
}

sourceCompatibility = JavaVersion.VERSION_11

def props = new Properties()
file('brewer-configuration/src/main/resources/application.properties').withInputStream {
    props.load(it)
}

ext {
    setProperty('springBootVersion', '2.3.4.RELEASE')
}

subprojects {
    group('io.bandrefilipe')
    version(props.getProperty('app.version'))

    apply(plugin: 'io.spring.dependency-management')
    apply(plugin: 'java-library')

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        // JUnit
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude(group: 'org.junit.vintage', module: 'junit-vintage-engine')
            exclude(group: 'org.springframework.boot', module: 'spring-boot-starter-logging')
        }
        // Lombok
        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    test {
        useJUnitPlatform()
    }
}

sonarqube {
    properties {
        setProperty('sonar.projectKey', 'bandrefilipe_brewer')
        setProperty('sonar.organization', 'bandrefilipe')
        setProperty('sonar.host.url', 'https://sonarcloud.io')
    }
}
