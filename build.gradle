plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'jacoco'
    id 'java'
    id 'org.sonarqube' version '3.0'
}

sourceCompatibility = JavaVersion.VERSION_11

def props = new Properties()
file('brewer-configuration/src/main/resources/application.properties').withInputStream {
    props.load(it)
}

ext {
    setProperty('springBootVersion', '2.3.4.RELEASE')
}

subprojects {
    group('io.bandrefilipe')
    version(props.getProperty('app.version'))

    apply(plugin: 'io.spring.dependency-management')
    apply(plugin: 'java-library')
    apply(plugin: 'jacoco')

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        // JUnit
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude(group: 'org.junit.vintage', module: 'junit-vintage-engine')
            exclude(group: 'org.springframework.boot', module: 'spring-boot-starter-logging')
        }
        // Lombok
        compileOnly('org.projectlombok:lombok')
        annotationProcessor('org.projectlombok:lombok')
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }

    sonarqube {
        properties {
            property 'sonar.junit.reportPaths', 'build/test-results/test'
            property 'sonar.sources', 'src/main/java'
            property 'sonar.tests', 'src/test/java'
            property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco'
        }
    }

    test {
        useJUnitPlatform()
        finalizedBy(jacocoTestReport)
    }

    jacocoTestReport {
        dependsOn(test)
        reports {
            xml.enabled(true)
        }
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.language', 'java'
        property 'sonar.organization', 'bandrefilipe'
        property 'sonar.projectKey', 'bandrefilipe_brewer'
        property 'sonar.projectVersion', props.getProperty('app.version')
        property 'sonar.sourceEncoding', 'UTF-8'
    }
}

project(":adapters:brewer-web") {
    sonarqube {
        skipProject = true
    }
}

project(":brewer-application") {
    sonarqube {
        skipProject = true
    }
}

project(":brewer-commons") {
    sonarqube {
        skipProject = true
    }
}
